plugins {
  id 'com.android.application'
  id 'kotlin-android'
  id "androidx.navigation.safeargs.kotlin"
  id "com.getkeepsafe.dexcount" version "3.0.1"
  id "com.google.protobuf" version "0.8.17"
}

android {
  compileSdkVersion 30
  defaultConfig {
    applicationId "com.tigeroakes.cellwall.client"
    minSdkVersion 19
    targetSdkVersion 30
    versionCode 5
    versionName "3.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    debug {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
    }
    release {
      minifyEnabled true
      shrinkResources true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }
  buildFeatures {
    viewBinding true
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = '1.8'
    freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
  }
  lintOptions {
    checkDependencies true
  }
}

protobuf {
  protoc {
    artifact = "com.google.protobuf:protoc:$protobuf_version"
  }

  // Generates the java Protobuf-lite code for the Protobufs in this project. See
  // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
  // for more information.
  generateProtoTasks {
    all().each { task ->
      task.builtins {
        java {
          option 'lite'
        }
      }
    }
  }
}


dependencies {
  implementation "com.google.android.material:material:$material_version"

  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

  implementation "androidx.appcompat:appcompat:$appcompat_version"
  implementation "androidx.core:core-ktx:$core_version"
  implementation "androidx.datastore:datastore:$datastore_version"
  implementation "androidx.datastore:datastore-core:$datastore_version"
  implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
  implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"

  implementation "com.google.protobuf:protobuf-javalite:$protobuf_version"
  implementation "com.squareup.retrofit2:retrofit:2.9.0"
  implementation "org.mozilla.geckoview:geckoview:$geckoview_version"

  testImplementation "junit:junit:4.13.2"
  testImplementation "io.mockk:mockk:1.12.0"
  testImplementation 'org.robolectric:robolectric:4.6.1'
  testImplementation "androidx.test:core-ktx:$core_test_version"
  testImplementation "androidx.test:runner:$runner_version"
  testImplementation "androidx.test.ext:junit-ktx:$junit_ktx_version"

  androidTestImplementation "androidx.test:core-ktx:$core_test_version"
  androidTestImplementation "androidx.test:runner:$runner_version"
  androidTestImplementation "androidx.test:rules:1.4.0"
  androidTestImplementation "androidx.test.ext:junit-ktx:$junit_ktx_version"
  androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}
